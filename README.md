This is fysh.

```fysh
><>
```

Every fysh has to be terminated `~`.

```fysh
><> ~
```

This is Steven.

```fysh
><steven> ~
```

Steven has binary scales. `}` represents 1 and `)` represents 0. Steven is
valued at `b101`. (5 in decimal)

```fysh
><steven> = ><})}> ~
```

Steven doesn’t give a flying fysh about scale direction. ≈ for variable
assignment is cool with them too.

```fysh
><steven> ≈ ><}({> ~
```

Steven is blind. You have the power to bless them with sight, but it's
completely optional. (you can give them multiple eyes)

```fysh
><steven> ≈ ><{({°> ~
><steven> ≈ ><{({o> ~

><steven> ≈ ><{({°°> ~
><steven> ≈ ><{({o°> ~

><steven> = ><{({°o°> ~
```

A school of fysh's value is equal to the sum of
each individual member. This gives Steven a value of `b101 = b100 + b001`.

```fysh
><steven> ≈ ><{((°> ><(({°> ~
```

Sometimes fysh want to be different and swim the other direction. This takes
away from the school’s value. This gives Steven a value of. 
`b101 = b111 - b010`.

```fysh
><steven> ≈ ><{{{°> <°)})>< ~
```

Fysh often get lonely. This loneliness causes fysh to meet new fysh and
proliferate. This gives Steven a value of `b101010 = b110 * b111`.

```fysh
><steven> = ><{{(°> <3 ><{{{°> ~
><steven> ≈ ><{{(o>  ♡ ><{{{o> ~

><steven> ≈ ><{{(o°> ❤️ ><{{{°°> ~
><steven> ≈ ><{{(oo> ❤️‍🔥><{{{°o> ~

><steven> ≈ ><{{(°°°> 🫀 ><{{{ooo> ~
><steven> ≈ ><{{(°o°> 🫶 ><{{{o°o> ~

.
.
.

><//> (works with any heart, if we're missing any, please let us know)
```


Not every fysh story is a happy one. At times, separation is unavoidable, and
their division breaks their heart. This gives Steven a value of `b101 = b11001 / b101` 

```fysh
><steven> ≈ ><{{(({°> 💔 ><{({°> ~
><steven> = ><{{(({o> </3 ><{({o> ~
```

Fysh are often abducted and forced into fyshbowls for terrestrial's entertainment. 
This isolates the fysh and makes them sad. Fyshbowls are represented by
`( )`. This gives Steven a value of `b101 = b101 / (b101 - b100)`.

```fysh
><steven> ≈ ><{({°> 💔 ( ><{({°> <°}))>< ) ~
```

As life goes on, we learn from our mistakes and improve. Steven’s self help
journey allowed them to grow an extra tail, incrementing their value by 1.

```fysh
>><steven> ~
```

Sometimes we feel like a fyshup, a failure. And that's ok, it’s a part of being
fysh. However for some fysh, this feeling is too much to handle and is
internalized. They haven’t received the emotional support they need and have
gone on a downward spiral, causing them to feel worthless. They begin to retreat
and try to swim away in the opposite direction causing their value to decrement
by 1.

```fysh
<steven><< ~
```

BEDMAS/PEMDAS has been taught to humans for over a century. But what about fysh? Fysh
have a different way of doing things. Introducing FISHS: Fyshbowl, Increment,
Species reproduction, Heartbreak, and School of fish. This gives Steven a value of
`b10101111 = (b11001 + 1) + b110 * b11001 - b1100 / (b110 + b110) `

```fysh
><steven> ≈ >><{{(({°>  ><{{(°> ♡ ><{{(({°> <°}}))>< 💔 ( ><{{(°> ><{{(°> )  ~
```

Sometimes Steven fyshs up and throws an error. This can be done using two WTF 
(What The Fysh) fysh encompassing a string.

```fysh
><!@#$>
	What The Fysh?!
<!@#$><
```

Navigator fysh are used to guide the reader through the code. They are
represented by `><//>`.

```fysh
><//> What's fyshin good lookin?
```

Sometimes, the journey through the code is long and arduous. In these cases,
multiline comments are used to provide a more detailed explanation of the code.
They are represented by `></*>` and `<*\><`.

```fysh
></*>
How to grill a fysh:
1. Catch a fysh
2. Grill the fysh
3. Eat the fysh
<*/><
```

This is a grilled fysh. It generates a random 32-bit number that determines the
doneness of the fysh.

```fysh
><###> ~
```

Not all fysh are created equal. But sometimes they are. We can check using
`≈≈` or `==`.

```fysh
><steven> ≈≈ ><theFysh> ~
><steven> == ><theFysh> ~
```

But sometimes we want to check if two fysh are different. This can be done
using `~≈` or `~=`.

```fysh
><steven> ~≈ ><theFysh> ~
><steven> ~= ><theFysh> ~
```

Tadpoles also exist in the fysh language. Tadpoles swim towards bigger fysh
(they like the danger) and are represented by `~o or o~`. Here we are checking if Steven is bigger than 5 and less than 7 respectively. Use `≈` or `=` for greater/less than or equal to.
```fysh
><//> Greater, Less than (respectively)
><steven> o~ ><{({°>
><steven> ~o ><{{{°>

><//> Greater, Less than or equal to (respectively)
><steven> o~≈ ><{({°>
><steven> ~o= ><{{{°>

><//> Greater, Less than or equal to (shorthand)
><steven> o≈ ><{({°>
><steven> =o ><{{{°>
```

Steven is a curious fysh and often wonders if the world is as it seems. They
ponder the meaning of life, the universe, and everything in between. They seek the truth,
and if both steven and theTruth are true, they find it. (Steven && theTruth). Or
if steven or theTruth is true, they find it. (Steven || theTruth). or if steven
is not real and this is all a simulation, they find it. (!!Steven)

```fysh
><steven> && ><theTruth> ~
><steven> || ><theTruth> ~
!! ><steven> ~
```

Steven is bored and looking to have their bits rearranged. At Fysh, we're here
to help! We have provided Steven with a variety of tools and bitwise
manipulations to satisfy their bit busting needs: `AND (&)`, `OR (|)`,
`XOR (^)`, `NOT (!)`. For those moments when they're feeling particularly
adventurous, they might even consider the `logical shift left (<<)` or
`logical shift right (>>)` operators.

```fysh
><steven> &  ><{((°> ~
><steven> |  ><{((°> ~
><steven> ^  ><{((°> ~
! ><steven> ~
><steven> << ><{((°> ~
><steven> >> ><{((°> ~
```

In the whirlpool of fysh logic, the while loop, symbolized by `><(((@>`,
ensnares conditions within `[ ]` , with `><>` and `<><` encapsulating the
iterative heart.

```fysh
><(((@> [ ><steven> o~ ><{((°> ]
><>
	<steven><< ~
<><
```

Navigating through decision streams, if and else statements flow naturally.
`><(((^>` marks the if, >`<(((*> ><(((^>` for else if, and `><(((*>` for the
uncharted else.

```fysh
><(((^> [ ><steven> o~ ><{((°> ]
><>
	<steven><< ~
<><

><(((*> ><(((^> [ ><steven> ~o ><{((°> ]
><>
	>><steven> ~
<><

><(((*>
><>
	><steven> ≈ ><(((°> ~
<><
```

A fysh tank `[ ]` is an array of fysh separated by fysh food `-`.

```fysh
><steven> ≈ [><({(°> - ><({(°>] ~
```

These fysh tanks can be traversed using a fysh.

```fysh
><steven>[><(({°>] ~
```

To define a SUBroutine named `submarine`, use `>(submarine)` along with any
parameters as Fysh. The last expression in the subroutine implicitly acts as the
return value. To explicitly return a value, use a baby squid `<~`.

```fysh
>(submarine) ><fysh> ><fysh2>
><>
	<~ <fysh>< ~
<><
```

To call a subroutine, put the submarine in a fysh tank along with its arguments
in any order:

```fysh
[><fysh2> >(submarine) ><fysh4>] ~
```

To interact with the ocean floor (GPIO), use the anchor.

```fysh
><{({°> o+) <fysh>< ><//>  get value from gpio pin b'101'
><{({°> (+o <fysh>< ><//> write to gpio pin b'101'
(+o ><fysh> ~ ><//> Write the value of Fysh to the GPIO pins
```

### Factorial Example

```fysh
><//> Comment

><number>    ≈ ><{({°> ~  ><//> b101 = 5
><factorial> ≈ ><(({°> ~  ><//> b001 = 1

><//> while number > 1
><(((@> [><number> o~ ><(({°>]
><>
	><//> factorial = factorial * number
	><factorial> ≈ ><factorial> ♡ ><number> ~

	><//> number -= 1
	<number><< ~
<><
```
